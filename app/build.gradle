apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.gyorgyzoltan.sprayApp"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "0.0.2"
        buildConfigField("String", "BUILD_DATE", "\"${new Date().format("yyyy-MM-dd")}\"")
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    signingConfigs {
        internal {
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile file("internal.keystore")
            storePassword "android"
        }
        release {
            keyAlias "androiddebugkey"
            keyPassword "android"
            storeFile file("internal.keystore")
            storePassword "android"
        }
    }
    buildFeatures {
        dataBinding = true
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.internal
        }
        internal {
            debuggable false
            minifyEnabled true
            shrinkResources true
            multiDexEnabled false
            applicationIdSuffix ".internal"
            versionNameSuffix "-internal"
            signingConfig signingConfigs.internal
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            matchingFallbacks = ["debug"]
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            multiDexEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2"

    // Networking
    //TODO: implementation "com.theapache64:retrosheet:1.2.1"

    // Service locator
    implementation "org.koin:koin-androidx-viewmodel:2.1.6"

    // Jetpack
    implementation "androidx.appcompat:appcompat:1.3.0-rc01"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0-alpha01"
    implementation "com.google.android.material:material:1.4.0-alpha01"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.recyclerview:recyclerview:1.2.0-rc01"

    // UI components
    implementation "com.romandanylyk:pageindicatorview:1.0.3"

    // Debug menu
    def beagleVersion = "2.5.1"
    def beagleUiType = "ui-drawer"
    debugImplementation "com.github.pandulapeter.beagle:$beagleUiType:$beagleVersion"
    debugImplementation "com.github.pandulapeter.beagle:log-crash:$beagleVersion"
    internalImplementation "com.github.pandulapeter.beagle:$beagleUiType:$beagleVersion"
    internalImplementation "com.github.pandulapeter.beagle:log-crash-noop:$beagleVersion"
    releaseImplementation "com.github.pandulapeter.beagle:noop:$beagleVersion"
    releaseImplementation "com.github.pandulapeter.beagle:log-crash-noop:$beagleVersion"
}